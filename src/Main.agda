{-# OPTIONS --without-K #-}

open import Agda.Builtin.Sigma using (Î£; snd) renaming (_,_ to infix 20 _,_)
open import Relation.Binary.PropositionalEquality as â‰¡ using (_â‰¡_; refl)
open import Data.Empty using (âŠ¥)
open import Data.Unit using (âŠ¤; tt)
open import Data.Product using (_Ã—_)
open import Data.Nat using (â„•; zero; suc)

data Ty : Set where
  Î¹ : Ty
  _âŸ¶_ : Ty -> Ty -> Ty
  â–¡_ : Ty -> Ty

infixr 30 _âŸ¶_
infix 30 â–¡_

-- Preterms
data Tm : Set where
  var : â„• -> Tm
  abs : Tm -> Tm
  app : Tm -> Tm -> Tm
  box : Tm -> Tm
  unbox : Tm -> Tm

-- Typing context
data Ctx : Set where
  Â· : Ctx
  _,_ : (Î“ : Ctx) -> (A : Ty) -> Ctx
  _,ğŸ”“ : (Î“ : Ctx) -> Ctx

â†ğŸ”“_ : Ctx -> Ctx
â†ğŸ”“ Â· = Â·
â†ğŸ”“ (x , A) = â†ğŸ”“ x
â†ğŸ”“ (x ,ğŸ”“) = x

ğŸ”“â†_ : Ctx -> Ctx
ğŸ”“â† Â· = Â·
ğŸ”“â† (x , A) = ğŸ”“â† x
ğŸ”“â† (x ,ğŸ”“) = x ,ğŸ”“

-- The type A can be found in the context at index n.
data Get (A : Ty) : Ctx -> â„• -> Set where
  zero : {Î“ : Ctx} -> Get A (Î“ , A) 0
  suc : {Î“ : Ctx} {n : â„•} {B : Ty} -> Get A Î“ n -> Get A (Î“ , B) (suc n)

_::_âˆˆ_ : â„• -> (A : Ty) -> (Î“ : Ctx) -> Set
n :: A âˆˆ Î“ = Get A Î“ n

-- Relation between contexts Î“ and Î“' signifying that it is possible
-- to extend Î“ to Î“', maybe adding any locks.
data Ext (ğŸ”“? : Set) (Î“ : Ctx) : Ctx -> Set where
  nil : Ext ğŸ”“? Î“ Î“
  snoc : {Î“' : Ctx} {A : Ty} -> Ext ğŸ”“? Î“ Î“' -> Ext ğŸ”“? Î“ (Î“' , A)
  snocğŸ”“ : {Î“' : Ctx} -> {ğŸ”“?} -> Ext ğŸ”“? Î“ Î“' -> Ext ğŸ”“? Î“ (Î“' ,ğŸ”“)

LFExt = Ext âŠ¥

â†ğŸ”“-of-lfExt-is-base : {Î“ Î“' : Ctx} -> LFExt (Î“ ,ğŸ”“) Î“' -> â†ğŸ”“ Î“' â‰¡ Î“
â†ğŸ”“-of-lfExt-is-base nil = refl
â†ğŸ”“-of-lfExt-is-base (snoc lfext) = â†ğŸ”“-of-lfExt-is-base lfext

-- Typing judgement: Term t is of type A in context Î“.
data _âŠ¢_::_ : Ctx -> Tm -> Ty -> Set where
  var : {n : â„•} {A : Ty} {Î“ : Ctx}
    -> n :: A âˆˆ Î“
    -> Î“ âŠ¢ var n :: A

  abs : {A B : Ty} {Î“ : Ctx} {t : Tm}
    -> Î“ , A âŠ¢ t :: B
    -> Î“ âŠ¢ abs t :: A âŸ¶ B

  app : {A B : Ty} {Î“ : Ctx} {t u : Tm}
    -> Î“ âŠ¢ t :: A âŸ¶ B -> Î“ âŠ¢ u :: A
    -> Î“ âŠ¢ app t u :: B

  box : {A : Ty} {Î“ : Ctx} {t : Tm}
    -> (Î“ ,ğŸ”“) âŠ¢ t :: A
    -> Î“ âŠ¢ box t :: (â–¡ A)

  unbox : {A : Ty} {Î“ Î“' : Ctx} {t : Tm}
    -> Î“ âŠ¢ t :: (â–¡ A)
    -> LFExt (Î“ ,ğŸ”“) Î“'
    -> Î“' âŠ¢ unbox t :: A

infix 10 _âŠ¢_::_

-- Order-preserving embedding (OPE).
--
-- For Î“ âŠ† Î”, Î” is weaker than Î“ since it has additional assumptions,
-- i.e. Î“ is a subsequence of Î”.
data _âŠ†_ : Ctx -> Ctx -> Set where
  base : Â· âŠ† Â·
  weak : âˆ€ {A Î“ Î”} -> Î“ âŠ† Î” -> Î“ âŠ† Î” , A
  lift : âˆ€ {A Î“ Î”} -> Î“ âŠ† Î” -> Î“ , A âŠ† Î” , A
  liftğŸ”“ : âˆ€ {Î“ Î”} -> Î“ âŠ† Î” -> Î“ ,ğŸ”“ âŠ† Î” ,ğŸ”“

infix 10 _âŠ†_

âŠ†-id : {Î“ : Ctx} -> Î“ âŠ† Î“
âŠ†-id {Â·} = base
âŠ†-id {Î“ , A} = lift âŠ†-id
âŠ†-id {Î“ ,ğŸ”“} = liftğŸ”“ âŠ†-id

-- Composition of weakenings (also transitivity proof).
_â—_ : {Î“ Î“' Î“'' : Ctx} -> Î“ âŠ† Î“' -> Î“' âŠ† Î“'' -> Î“ âŠ† Î“''
x â— base = x
x â— (weak y) = weak (x â— y)
(weak x) â— (lift y) = weak (x â— y)
(lift x) â— (lift y) = lift (x â— y)
(liftğŸ”“ x) â— (liftğŸ”“ y) = liftğŸ”“ (x â— y)

-- Drop the part of the OPE that pertains to the context extension
rewind-âŠ† : {Î“ Î“' Î“'' : Ctx} -> LFExt (Î“' ,ğŸ”“) Î“ -> Î“ âŠ† Î“'' -> Î“' âŠ† â†ğŸ”“ Î“''
rewind-âŠ† lfext (weak w) = rewind-âŠ† lfext w
rewind-âŠ† (snoc lfext) (lift w) = rewind-âŠ† lfext w
rewind-âŠ† nil (liftğŸ”“ w) = w

lfext-to-âŠ† : {Î“ Î“' : Ctx} -> LFExt Î“ Î“' -> Î“ âŠ† Î“'
lfext-to-âŠ† nil = âŠ†-id
lfext-to-âŠ† (snoc x) = weak (lfext-to-âŠ† x)

wkLFExt : {Î“L Î“ Î” : Ctx} -> Î“ âŠ† Î” -> LFExt (Î“L ,ğŸ”“) Î“ -> LFExt ((â†ğŸ”“ Î”) ,ğŸ”“) Î”
wkLFExt (weak w) e = snoc (wkLFExt w e)
wkLFExt (lift w) (snoc e) = snoc (wkLFExt w e)
wkLFExt (liftğŸ”“ w) e = nil

wk-var : âˆ€ {Î“ Î” A n} -> (w : Î“ âŠ† Î”) -> n :: A âˆˆ Î“ -> Î£ â„• (_:: A âˆˆ Î”)
wk-var {n = n} base x = n , x
wk-var (weak w) x = let m , y = wk-var w x in suc m , suc y
wk-var (lift w) zero = 0 , zero
wk-var (lift w) (suc x) = let m , y = wk-var w x in suc m , suc y

-- Variable weakening
wk : âˆ€ {Î“ Î” t A} -> (w : Î“ âŠ† Î”)
  -> Î“ âŠ¢ t :: A -> Î£ Tm Î» t' -> Î” âŠ¢ t' :: A
wk w (var x) = let m , y = wk-var w x in var m , var y
wk w (abs t) = let t' , x = wk (lift w) t in abs t' , abs x
wk w (app t s) = let
  t' , x = wk w t
  s' , y = wk w s
  in app t' s' , app x y
wk w (box t) = let t' , x = wk (liftğŸ”“ w) t in box t' , box x
wk {Î” = Î”} {A = A} w (unbox t lfext) = let
  t' , x = wk (rewind-âŠ† lfext w) t
  in unbox t' , unbox x (wkLFExt w lfext)

-- Substitution from variables in context Î“ to terms in context Î”.
data Sub : Ctx -> Ctx -> Set where
  base : {Î” : Ctx} -> Sub Â· Î”
  sub : {Î“ Î” : Ctx} {A : Ty} {t : Tm}
    -> (Ïƒ : Sub Î“ Î”)
    -> Î” âŠ¢ t :: A
    -> Sub (Î“ , A) Î”
  lock : {Î“ Î” Î”' : Ctx} -> (Ïƒ : Sub Î“ Î”) -> LFExt (Î” ,ğŸ”“) Î”' -> Sub (Î“ ,ğŸ”“) Î”'

wkSub : {Î“ Î” Î”' : Ctx} -> Î” âŠ† Î”' -> Sub Î“ Î” -> Sub Î“ Î”'
wkSub w base = base
wkSub w (sub Ïƒ x) = sub (wkSub w Ïƒ) (snd (wk w x))
wkSub w (lock Ïƒ ext) = lock (wkSub (rewind-âŠ† ext w) Ïƒ) (wkLFExt w ext)

lift-sub : {Î“ Î” : Ctx} {A : Ty} -> Sub Î“ Î” -> Sub (Î“ , A) (Î” , A)
lift-sub Ïƒ = sub (wkSub (weak âŠ†-id) Ïƒ) (var zero)

id-sub : {Î“ : Ctx} -> Sub Î“ Î“
id-sub {Â·} = base
id-sub {Î“ , A} = lift-sub id-sub
id-sub {Î“ ,ğŸ”“} = lock id-sub nil

subst : {Î“ Î” : Ctx} {A : Ty} {t : Tm}
  -> Sub Î“ Î” -> Î“ âŠ¢ t :: A -> Î£ Tm Î» t' -> Î” âŠ¢ t' :: A
subst Ïƒ (abs x) = let t , y = subst (lift-sub Ïƒ) x in abs t , abs y
subst Ïƒ (app x y) = let
  t , x' = subst Ïƒ x
  s , y' = subst Ïƒ y
  in app t s , app x' y'
subst Ïƒ (box x) = let t , y = subst (lock Ïƒ nil) x in box t , box y
subst Ïƒ (unbox x ext) = let t , y = subst (rewind-sub ext Ïƒ) x
  in unbox t , unbox y (rewind-sub-ext ext Ïƒ)
  where
    -- With the pair of contexts (Î“', Î”) and an extension from Î“ to Î“',
    -- rewind Î” back for as many locks as there are in the extension.

    rewind-sub-ty : {Î“ Î“' Î” : Ctx} -> (e : LFExt (Î“ ,ğŸ”“) Î“') -> (Ïƒ : Sub Î“' Î”) -> Ctx
    rewind-sub-ty nil (lock {Î” = Î”} _Ïƒ _ext) = Î”
    rewind-sub-ty (snoc e) (sub Ïƒ _) = rewind-sub-ty e Ïƒ

    rewind-sub-ext : {Î“ Î“' Î” : Ctx} -> (e : LFExt (Î“ ,ğŸ”“) Î“') -> (Ïƒ : Sub Î“' Î”) -> LFExt ((rewind-sub-ty e Ïƒ) ,ğŸ”“) Î”
    rewind-sub-ext nil (lock {Î” = Î”} _Ïƒ ext) = ext
    rewind-sub-ext (snoc e) (sub Ïƒ _) = rewind-sub-ext e Ïƒ

    rewind-sub : {Î“ Î“' Î” : Ctx} -> (e : LFExt (Î“ ,ğŸ”“) Î“') -> (Ïƒ : Sub Î“' Î”) -> Sub Î“ (rewind-sub-ty e Ïƒ)
    rewind-sub nil (lock Ïƒ ext) = Ïƒ
    rewind-sub (snoc lfext) (sub Ïƒ x) = rewind-sub lfext Ïƒ
subst (sub {t = t'} Ïƒ x) (var zero) = t' , x
subst (sub Ïƒ x) (var (suc g)) = subst Ïƒ (var g)

-- Applies unit substitution.
_[_] : {Î“ : Ctx} {t s : Tm} {A B : Ty}
  -> Î“ , B âŠ¢ t :: A
  -> Î“ âŠ¢ s :: B
  -> Î£ Tm Î» t' -> Î“ âŠ¢ t' :: A
_[_] x y = subst (sub id-sub y) x

-- Equivalence of terms-in-context
data _â‰…_ : {Î“ : Ctx} {t s : Tm} {A : Ty}
  -> Î“ âŠ¢ t :: A -> Î“ âŠ¢ s :: A -> Set where
  â‰…-refl : âˆ€ {Î“ t A} {x : Î“ âŠ¢ t :: A}
    -> x â‰… x
  â‰…-sym : âˆ€ {Î“ t s A} {x : Î“ âŠ¢ t :: A} {y : Î“ âŠ¢ s :: A}
    -> x â‰… y -> y â‰… x
  â‰…-trans : âˆ€ {Î“ t s u A} {x : Î“ âŠ¢ t :: A} {y : Î“ âŠ¢ s :: A} {z : Î“ âŠ¢ u :: A}
    -> x â‰… y -> y â‰… z -> x â‰… z

  Î² : âˆ€ {Î“ t A B} -> (x : Î“ , A âŠ¢ t :: B) -> (y : Î“ âŠ¢ t :: A)
    -> app (abs x) y â‰… snd (x [ y ])
  Î· : âˆ€ {Î“ t A B} {x : Î“ âŠ¢ t :: A âŸ¶ B}
    -> x â‰… abs (app (snd (wk (weak âŠ†-id) x)) (var zero))

  â–¡-red : âˆ€ {Î“ Î“' t A} {x : Î“ ,ğŸ”“ âŠ¢ t :: A} {ext : LFExt (Î“ ,ğŸ”“) Î“'}
    -> unbox (box x) ext â‰… snd (wk (lfext-to-âŠ† ext) x)
  â–¡-conv : âˆ€ {Î“ t A} -> {x : Î“ âŠ¢ t :: â–¡ A}
    -> x â‰… box (unbox x nil)

  -- Congruence rules
  cong-abs : âˆ€ {Î“ t t' A B} {x : Î“ , A âŠ¢ t :: B} {y : Î“ , A âŠ¢ t' :: B}
    -> x â‰… y -> abs x â‰… abs y

mutual
  -- Normal forms
  data _âŠ¢nf_ (Î“ : Ctx) : Ty -> Set where
    nt : {A : Ty} -> Î“ âŠ¢nt A -> Î“ âŠ¢nf A
    abs : {A B : Ty} -> Î“ , A âŠ¢nf B -> Î“ âŠ¢nf A âŸ¶ B
    box : {A : Ty} -> Î“ ,ğŸ”“ âŠ¢nf A -> Î“ âŠ¢nf â–¡ A
  -- Neutral terms
  data _âŠ¢nt_ (Î“ : Ctx) : Ty -> Set where
    var : {A : Ty} -> {n : â„•} -> Get A Î“ n -> Î“ âŠ¢nt A
    app : {A B : Ty} -> Î“ âŠ¢nt A âŸ¶ B -> Î“ âŠ¢nf A -> Î“ âŠ¢nt B
    unbox : {A : Ty} {Î“' : Ctx} -> Î“' âŠ¢nt â–¡ A -> LFExt (Î“' ,ğŸ”“) Î“ -> Î“ âŠ¢nt A

infix 10 _âŠ¢nf_ _âŠ¢nt_

wk-nf : {Î“ Î” : Ctx} {A : Ty} -> Î“ âŠ† Î” -> Î“ âŠ¢nf A -> Î” âŠ¢nf A
wk-nt : {Î“ Î” : Ctx} {A : Ty} -> Î“ âŠ† Î” -> Î“ âŠ¢nt A -> Î” âŠ¢nt A
wk-nf w (nt x) = nt (wk-nt w x)
wk-nf w (abs x) = abs (wk-nf (lift w) x)
wk-nf w (box x) = box (wk-nf (liftğŸ”“ w) x)
wk-nt w (var x) = var (snd (wk-var w x))
wk-nt w (app x y) = app (wk-nt w x) (wk-nf w y)
wk-nt w (unbox x e) = unbox (wk-nt (rewind-âŠ† e w) x) (wkLFExt w e)

-- Natural transformation between presheafs
_â†’Ì‡_ : (Ctx â†’ Set) â†’ (Ctx â†’ Set) â†’ Set
_â†’Ì‡_ A B = {Î” : Ctx} â†’ A Î” â†’ B Î”

record Box' (A' : Ctx -> Set) (Î“ : Ctx) : Set where
  constructor box'
  field
    unbox' : A' (Î“ ,ğŸ”“)

-- Interpret a type to a presheaf
âŸ¦_âŸ§ty : Ty -> Ctx -> Set
âŸ¦ Î¹ âŸ§ty = _âŠ¢nf Î¹
âŸ¦ A âŸ¶ B âŸ§ty Î“ = {Î” : Ctx} -> Î“ âŠ† Î” -> âŸ¦ A âŸ§ty Î” -> âŸ¦ B âŸ§ty Î”
âŸ¦ â–¡ A âŸ§ty Î“ = Box' âŸ¦ A âŸ§ty Î“

-- Interpret context to a presheaf
data Env : (Î“ Î” : Ctx) -> Set where
  Â· : {Î” : Ctx} -> Env Â· Î”
  _,_ : {Î“ Î” : Ctx} {A : Ty} -> Env Î“ Î” -> âŸ¦ A âŸ§ty Î” -> Env (Î“ , A) Î”
  lock : {Î“ Î” Î”' : Ctx} -> Env Î“ Î” -> LFExt (Î” ,ğŸ”“) Î”' -> Env (Î“ ,ğŸ”“) Î”'

âŸ¦_âŸ§ctx = Env

wk-ty' : {A : Ty} {Î“ Î” : Ctx} -> Î“ âŠ† Î” -> âŸ¦ A âŸ§ty Î“ -> âŸ¦ A âŸ§ty Î”
wk-ty' {Î¹} w A' = wk-nf w A'
wk-ty' {A âŸ¶ B} w AâŸ¶B' w2 A' = AâŸ¶B' (w â— w2) A'
wk-ty' {â–¡ A} w (box' A') = box' (wk-ty' {A} (liftğŸ”“ w) A')

wk-env : {Î“ Î” Î”' : Ctx} -> Î” âŠ† Î”' -> âŸ¦ Î“ âŸ§ctx Î” -> âŸ¦ Î“ âŸ§ctx Î”'
wk-env {Â·} w Â· = Â·
wk-env {Î“ , A} w (Î“' , A') = wk-env {Î“} w Î“' , wk-ty' {A} w A'
wk-env {Î“ ,ğŸ”“} w (lock Î“' e) = lock (wk-env (rewind-âŠ† e w) Î“') (wkLFExt w e)

-- Interpret terms-in-contexts as natural transformations
âŸ¦_âŸ§tm : {Î“ : Ctx} {t : Tm} {A : Ty} -> Î“ âŠ¢ t :: A -> {Î” : Ctx} -> âŸ¦ Î“ âŸ§ctx Î” -> âŸ¦ A âŸ§ty Î”
âŸ¦ var AâˆˆÎ“ âŸ§tm Î“' = lookup AâˆˆÎ“ Î“'
  where
    lookup : âˆ€ {A Î“ n} {Î” : Ctx} -> Get A Î“ n -> âŸ¦ Î“ âŸ§ctx Î” -> âŸ¦ A âŸ§ty Î”
    lookup zero (_ , A') = A'
    lookup (suc x) (Î“' , _) = lookup x Î“'
âŸ¦ abs x âŸ§tm Î“' e y' = âŸ¦ x âŸ§tm (wk-env e Î“' , y')
âŸ¦ app x y âŸ§tm Î“' = âŸ¦ x âŸ§tm Î“' âŠ†-id (âŸ¦ y âŸ§tm Î“')
âŸ¦ box x âŸ§tm Î“' = box' (âŸ¦ x âŸ§tm (lock Î“' nil))
âŸ¦_âŸ§tm {A = A} (unbox x e) Î“' = let box' y' = âŸ¦ x âŸ§tm (rewind-env e Î“')
  in wk-ty' {A} (lfext-to-âŠ† (â†ğŸ”“-lfext e Î“')) y'
  where
    â†ğŸ”“-lfext : {Î“ Î“' Î” : Ctx} -> LFExt (Î“ ,ğŸ”“) Î“' -> Env Î“' Î” -> LFExt ((â†ğŸ”“ Î”) ,ğŸ”“) Î”
    â†ğŸ”“-lfext (snoc e) (env , _) = â†ğŸ”“-lfext e env
    â†ğŸ”“-lfext nil (lock env nil) = nil
    â†ğŸ”“-lfext nil (lock env (snoc lfext)) = snoc (â†ğŸ”“-lfext nil (lock env lfext))

    rewind-env : {Î“ Î“' Î” : Ctx} -> LFExt (Î“ ,ğŸ”“) Î“' -> Env Î“' Î” -> Env Î“ (â†ğŸ”“ Î”)
    rewind-env (snoc e) (env , _) = rewind-env e env
    rewind-env nil (lock env lfext) rewrite â†ğŸ”“-of-lfExt-is-base lfext = env

reify : {A : Ty} {Î“ : Ctx} -> âŸ¦ A âŸ§ty Î“ -> Î“ âŠ¢nf A
reflect : {A : Ty} {Î“ : Ctx} -> Î“ âŠ¢nt A -> âŸ¦ A âŸ§ty Î“
reify {Î¹} A' = A'
reify {A âŸ¶ B} AâŸ¶B' = abs (reify (AâŸ¶B' (weak âŠ†-id) (reflect {A} (var zero))))
reify {â–¡ A} (box' A') = box (reify A')
reflect {Î¹} x = nt x
reflect {A âŸ¶ B} x e A' = reflect (app (wk-nt e x) (reify A'))
reflect {â–¡ A} x = box' (reflect (unbox x nil))

-- Normalization function
nf : {Î“ : Ctx} {t : Tm} {A : Ty} -> Î“ âŠ¢ t :: A -> Î“ âŠ¢nf A
nf x = reify (âŸ¦ x âŸ§tm Î“')
  where
    -- Initial environment consisting of all neutrals
    Î“' : {Î“ : Ctx} -> âŸ¦ Î“ âŸ§ctx Î“
    Î“' {Â·} = Â·
    Î“' {Î“ , A} = wk-env (weak âŠ†-id) Î“' , reflect {A} (var zero)
    Î“' {Î“ ,ğŸ”“} = lock Î“' nil
