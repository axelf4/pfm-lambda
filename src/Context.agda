{-# OPTIONS --without-K --safe #-}

module Context where

open import Agda.Builtin.Sigma using (Î£; snd) renaming (_,_ to infix 20 _,_)
open import Relation.Binary.PropositionalEquality as â‰¡ using (_â‰¡_; refl; cong)
open import Data.Empty using (âŠ¥)
open import Data.Product using (_Ã—_)

open import Util using (cong1)

data Ty : Set where
  Î¹ : Ty
  _âŸ¶_ : Ty -> Ty -> Ty
  â–¡_ : Ty -> Ty

infixr 30 _âŸ¶_
infix 30 â–¡_

-- Typing context
data Ctx : Set where
  Â· : Ctx
  _,_ : (Î“ : Ctx) -> (A : Ty) -> Ctx
  _,ðŸ”“ : (Î“ : Ctx) -> Ctx

-- The type A can be found in the context at index n.
data _âˆˆ_ (A : Ty) : Ctx -> Set where
  zero : {Î“ : Ctx} -> A âˆˆ (Î“ , A)
  suc : {Î“ : Ctx} {B : Ty} -> A âˆˆ Î“ -> A âˆˆ (Î“ , B)

-- Relation between contexts Î“ and Î“' signifying that it is possible
-- to extend Î“ to Î“', maybe adding any locks.
data Ext (ðŸ”“? : Set) (Î“ : Ctx) : Ctx -> Set where
  nil : Ext ðŸ”“? Î“ Î“
  snoc : {Î“' : Ctx} {A : Ty} -> Ext ðŸ”“? Î“ Î“' -> Ext ðŸ”“? Î“ (Î“' , A)
  snocðŸ”“ : {Î“' : Ctx} -> {ðŸ”“?} -> Ext ðŸ”“? Î“ Î“' -> Ext ðŸ”“? Î“ (Î“' ,ðŸ”“)

LFExt = Ext âŠ¥
{-# DISPLAY Ext âŠ¥ = LFExt #-}

-- Order-preserving embedding (OPE).
--
-- For Î“ âŠ† Î”, Î” is weaker than Î“ since it has additional assumptions,
-- i.e. Î“ is a subsequence of Î”.
data _âŠ†_ : Ctx -> Ctx -> Set where
  base : Â· âŠ† Â·
  weak : âˆ€ {A Î“ Î”} -> Î“ âŠ† Î” -> Î“ âŠ† Î” , A
  lift : âˆ€ {A Î“ Î”} -> Î“ âŠ† Î” -> Î“ , A âŠ† Î” , A
  liftðŸ”“ : âˆ€ {Î“ Î”} -> Î“ âŠ† Î” -> Î“ ,ðŸ”“ âŠ† Î” ,ðŸ”“

infix 10 _âŠ†_

-- Composition of weakenings (and transitivity proof)
_â—_ : {Î“ Î“' Î“'' : Ctx} -> Î“ âŠ† Î“' -> Î“' âŠ† Î“'' -> Î“ âŠ† Î“''
x â— base = x
x â— (weak y) = weak (x â— y)
(weak x) â— (lift y) = weak (x â— y)
(lift x) â— (lift y) = lift (x â— y)
(liftðŸ”“ x) â— (liftðŸ”“ y) = liftðŸ”“ (x â— y)

module âŠ† where
  id : {Î“ : Ctx} -> Î“ âŠ† Î“
  id {Â·} = base
  id {Î“ , A} = lift id
  id {Î“ ,ðŸ”“} = liftðŸ”“ id

  idl : {Î“ Î” : Ctx} {w : Î“ âŠ† Î”} -> id â— w â‰¡ w
  idl {w = base} = refl
  idl {w = weak w} = cong weak idl
  idl {w = lift w} = cong lift idl
  idl {w = liftðŸ”“ w} = cong liftðŸ”“ idl

  idr : {Î“ Î” : Ctx} {w : Î“ âŠ† Î”} -> w â— id â‰¡ w
  idr {w = base} = refl
  idr {w = weak w} = cong weak idr
  idr {w = lift w} = cong lift idr
  idr {w = liftðŸ”“ w} = cong liftðŸ”“ idr

wkVar : {A : Ty} {Î“ Î” : Ctx} -> Î“ âŠ† Î” -> A âˆˆ Î“ -> A âˆˆ Î”
wkVar base x = x
wkVar (weak w) x = suc (wkVar w x)
wkVar (lift w) zero = zero
wkVar (lift w) (suc x) = suc (wkVar w x)

wkVarId : {A : Ty} {Î“ : Ctx} -> (x : A âˆˆ Î“) -> wkVar âŠ†.id x â‰¡ x
wkVarId zero = refl
wkVarId (suc x) = cong suc (wkVarId x)

module Replacement (_â—_ : Ctx -> Ctx -> Set) (F : Ty -> Ctx -> Set) where
  -- For every item in context Î“ there is a replacement value in context Î”.
  data Rpl : Ctx -> Ctx -> Set where
    Â· : {Î” : Ctx} -> Rpl Â· Î”
    _,_ : {Î“ Î” : Ctx} {A : Ty} -> Rpl Î“ Î” -> F A Î” -> Rpl (Î“ , A) Î”
    lock : {Î“ Î” Î”' : Ctx} -> Rpl Î“ Î” -> Î” â— Î”' -> Rpl (Î“ ,ðŸ”“) Î”'

  module Properties
    (â—1 : {Î“ : Ctx} -> Î“ â— (Î“ ,ðŸ”“))
    (rewind-âŠ† : {Î“ Î“' Î” : Ctx} -> (m : Î“' â— Î“) -> Î“ âŠ† Î”
      -> Î£ Ctx Î» Î”' -> Î”' â— Î” Ã— Î“' âŠ† Î”')
    (wkF : {A : Ty} {Î“ Î“' : Ctx} -> Î“ âŠ† Î“' -> F A Î“ -> F A Î“')
    (head : {A : Ty} {Î“ : Ctx} -> F A (Î“ , A))
    where

    -- Composition of substitution and weakening
    wk : {Î“ Î” Î”' : Ctx} -> Î” âŠ† Î”' -> Rpl Î“ Î” -> Rpl Î“ Î”'
    wk w Â· = Â·
    wk w (Ïƒ , x) = wk w Ïƒ , wkF w x
    wk w (lock Ïƒ m)
      = let _ , (m' , w') = rewind-âŠ† m w in lock (wk w' Ïƒ) m'

    -- Composition of weakening and substitution
    trim : {Î“ Î“' Î” : Ctx} -> Î“ âŠ† Î“' -> Rpl Î“' Î” -> Rpl Î“ Î”
    trim base Â· = Â·
    trim (weak w) (Ïƒ , _) = trim w Ïƒ
    trim (lift w) (Ïƒ , x) = trim w Ïƒ , x
    trim (liftðŸ”“ w) (lock Ïƒ m) = lock (trim w Ïƒ) m

    drop : {Î“ Î” : Ctx} {A : Ty} -> Rpl Î“ Î” -> Rpl Î“ (Î” , A)
    drop = wk (weak âŠ†.id)

    liftRpl : {Î“ Î” : Ctx} {A : Ty} -> Rpl Î“ Î” -> Rpl (Î“ , A) (Î” , A)
    liftRpl Ïƒ = drop Ïƒ , head

    id : {Î“ : Ctx} -> Rpl Î“ Î“
    id {Â·} = Â·
    id {x , A} = liftRpl id
    id {x ,ðŸ”“} = lock id â—1

    from-âŠ† : {Î“ Î” : Ctx} -> Î“ âŠ† Î” -> Rpl Î“ Î”
    from-âŠ† base = Â·
    from-âŠ† (weak w) = drop (from-âŠ† w)
    from-âŠ† (lift w) = from-âŠ† (weak w) , head
    from-âŠ† (liftðŸ”“ w) = lock (from-âŠ† w) â—1

    trimNat : {Î“ Î“' Î” Î”' : Ctx} (w : Î“' âŠ† Î“) (w' : Î” âŠ† Î”') (Ïƒ : Rpl Î“ Î”)
      -> wk w' (trim w Ïƒ) â‰¡ trim w (wk w' Ïƒ)
    trimNat base w' Â· = refl
    trimNat (weak w) w' (Ïƒ , x) = trimNat w w' Ïƒ
    trimNat (lift w) w' (Ïƒ , x) = cong1 _,_ (trimNat w w' Ïƒ)
    trimNat (liftðŸ”“ w) w' (lock Ïƒ m) = cong1 lock (trimNat w _ Ïƒ)

    trimIdl : {Î“ Î” : Ctx} -> (Ïƒ : Rpl Î“ Î”) -> trim âŠ†.id Ïƒ â‰¡ Ïƒ
    trimIdl Â· = refl
    trimIdl (s , x) = cong (_, x) (trimIdl s)
    trimIdl (lock s m) = cong1 lock (trimIdl s)

    trimIdr : {Î“ Î” : Ctx} -> (w : Î“ âŠ† Î”) -> trim w id â‰¡ from-âŠ† w
    trimIdr base = refl
    trimIdr (weak w) = â‰¡.trans
      (â‰¡.sym (trimNat w (weak âŠ†.id) id))
      (cong drop (trimIdr w))
    trimIdr (lift w) = cong (_, head) (â‰¡.trans
      (â‰¡.sym (trimNat w (weak âŠ†.id) id))
      (cong drop (trimIdr w)))
    trimIdr (liftðŸ”“ w) = cong1 lock (trimIdr w)

  module Composition
    (rewind : {Î“ Î“' Î” : Ctx} -> (m : Î“' â— Î“) -> Rpl Î“ Î”
      -> Î£ Ctx Î» Î”' -> Î”' â— Î” Ã— Rpl Î“' Î”')
    (apply : {A : Ty} {Î“ Î” : Ctx} -> Rpl Î“ Î” -> F A Î“ -> F A Î”)
    where
    _âˆ™_ : {Î“ Î“' Î“'' : Ctx} -> Rpl Î“ Î“' -> Rpl Î“' Î“'' -> Rpl Î“ Î“''
    Â· âˆ™ y = Â·
    (x , a) âˆ™ y = (x âˆ™ y) , apply y a
    lock x m âˆ™ y
      = let _ , (m' , y') = rewind m y in lock (x âˆ™ y') m'

module _ {_â—_ : Ctx -> Ctx -> Set} where
  open Replacement _â—_ using (Rpl; Â·; _,_; lock)

  mapRpl : {F G : Ty -> Ctx -> Set} -> ({A : Ty} {Î“ : Ctx} -> F A Î“ -> G A Î“)
    -> {Î“ Î” : Ctx} -> Rpl F Î“ Î” -> Rpl G Î“ Î”
  mapRpl f Â· = Â·
  mapRpl f (Ïƒ , x) = mapRpl f Ïƒ , f x
  mapRpl f (lock Ïƒ m) = lock (mapRpl f Ïƒ) m
